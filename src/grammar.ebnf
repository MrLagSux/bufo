File: Fn*;

Fn = "func" "name" ParamList ["->" ReturnType] Block;

Param = "name" TypeDecl [","];
ParamList = "(" {Param} ")";

Block = "{" {Stmt} "}";

Stmt = StmtExpr
     | StmtLet
     | StmtAssign
     | StmtIf
     | StmtReturn;

StmtExpr = Expr ";";
StmtLet = "let" "name" TypeDecl "=" Expr ";";
StmtAssign = "name" "=" Expr ";";
StmtIf = "if" "(" Expr ")" Block {"else" Block};
StmtReturn = "return" [Expr] ";";

ReturnType = "type";
TypeDecl = ":" "type";

Arg = Expr [","];
ArgList = "(" {Arg} ")";

Expr = ExprName
     | ExprLiteral
     | ExprBinary
     | ExprParen
     | ExprCall;
ExprName = "name";
ExprLiteral = "int";
ExprBinary = Expr ("+" | "-" | "*" | "/" | "==" | "!=" | "<" | ">" | "<=" | ">=") Expr;
ExprParen = "(" Expr ")";
ExprCall = "name" ArgList;
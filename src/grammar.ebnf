File = {Struct} {Fn};

Struct = "struct" "StructName" FieldList;
FieldList = "{" Field {"," Field} [","] "}";
Field = "FieldName" TypeDecl;

Fn = "func" "name" ParamList ["->" Type] Block;

Param = "name" TypeDecl;
ParamList = "(" [Param {"," Param}] ")";

Block = "{" {Stmt} "}";

Stmt = StmtExpr
     | StmtLet
     | StmtAssign
     | StmtIf
     | StmtReturn;

StmtExpr = ExprCall ";";
StmtLet = "let" "name" TypeDecl "=" (Expr | ExprArrayLiteral) ";";
StmtAssign = ("name" | ExprStructAccess | ExprArray) "=" Expr ";";
StmtIf = "if" "(" ExprComp ")" Block ["else" Block];
StmtReturn = "return" [Expr] ";";

TypeDecl = ":" Type;
Type = "i32" | "i64" | "u32" | "u64" | "usize" | "StructName";

Arg = Expr;
ArgList = "(" [Arg {"," Arg}] ")";

Expr = ExprName
     | ExprArrayAccess
     | ExprLiteral
     | ExprBinary
     | ExprComp
     | ExprParen
     | ExprCall
     | ExprStructDecl
     | ExprStructAccess;
ExprName = "name";
ExprArrayLiteral = "[" Expr {"," Expr} "]";
ExprArrayAccess = "name" ExprArrayLiteral;
ExprLiteral = "int";
ExprBinary = Expr ("+" | "-" | "*" | "/") Expr;
ExprComp = Expr ("==" | "!=" | "<" | ">" | "<=" | ">=") Expr;
ExprParen = "(" Expr ")";
ExprCall = "name" ArgList;

ExprStructDecl = "StructName" "{" FieldDecl {"," FieldDecl} "}";
FieldDecl = "FieldName" ":" Expr;
ExprStructAccess = "StructName" "." "FieldName";